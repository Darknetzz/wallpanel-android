/*
 * Copyright (c) 2022 WallPanel
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed
 * under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt' // Allows for Kotlin annotation processors
    //id 'androidx.navigation.safeargs.kotlin'
    //id 'dagger.hilt.android.plugin'
    // Google Services and Crashlytics applied conditionally below when google-services.json is present
}

def versionMajor = 0
def versionMinor = 12
def versionPatch = 0
def versionBuild = 0 // bump for dog food builds, public betas, etc.

def safeLocalProperty(String key, String defaultValue) {
    def file = project.rootProject.file('local.properties')
    if (!file.exists()) return defaultValue
    def properties = new Properties()
    try {
        properties.load(file.newDataInputStream())
        def value = properties.getProperty(key)
        return value != null ? value : defaultValue
    } catch (Exception ignored) {
        return defaultValue
    }
}

def CODE() { return safeLocalProperty("code", "1234") }
def HASS_URL() { return safeLocalProperty("hassUrl", "") }
def BROKER() { return safeLocalProperty("broker", "") }
def BROKER_PASS() { return safeLocalProperty("brokerPass", "") }
def BROKER_USERNAME() { return safeLocalProperty("brokerUsername", "") }

// Use JDK 17 for compilation so kapt/Kotlin work when running Gradle on JDK 25
kotlin {
    jvmToolchain(17)
}

android {
    kapt {
        correctErrorTypes = true
        javacOptions {
            option("--release", "17")
        }
    }
    compileSdk 34
    lint {
        checkReleaseBuilds = false
    }
    defaultConfig {
        applicationId "xyz.wallpanel.app"
        minSdkVersion 19
        targetSdk 34
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    splits {
        abi {
            reset()
            enable true
            universalApk true
            include "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }

    flavorDimensions "default"

    productFlavors {
        def BASE_ENVIRONMENT = "BASE_ENVIRONMENT"
        dev {
            dimension "default"
            buildConfigField "String", BASE_ENVIRONMENT, '"DEV_ENVIRONMENT"'
            applicationId "xyz.wallpanel.app"
            versionName "${versionMajor}.${versionMinor}.${versionPatch} Build ${versionBuild}-DEV"
            buildConfigField 'Integer', 'CODE', CODE()
            buildConfigField 'String', 'HASS_URL', "\"" + HASS_URL().replace('"', '\\"') + "\""
            buildConfigField 'String', 'BROKER', "\"" + BROKER().replace('"', '\\"') + "\""
            buildConfigField 'String', 'BROKER_PASS', "\"" + BROKER_PASS().replace('"', '\\"') + "\""
            buildConfigField 'String', 'BROKER_USERNAME', "\"" + BROKER_USERNAME().replace('"', '\\"') + "\""
        }
        qa {
            dimension "default"
            buildConfigField "String", BASE_ENVIRONMENT, '"QA_ENVIRONMENT"'
            applicationId "xyz.wallpanel.app"
            versionName "${versionMajor}.${versionMinor}.${versionPatch} Build ${versionBuild}-QA"
            buildConfigField 'Integer', 'CODE', '1234'
            buildConfigField 'String', 'HASS_URL', '""'
            buildConfigField 'String', 'BROKER', '""'
            buildConfigField 'String', 'BROKER_USERNAME', '""'
            buildConfigField 'String', 'BROKER_PASS', '""'
        }
        prod {
            dimension "default"
            buildConfigField "String", BASE_ENVIRONMENT, '"PROD_ENVIRONMENT"'
            applicationId "xyz.wallpanel.app"
            versionName "${versionMajor}.${versionMinor}.${versionPatch} Build ${versionBuild}"
            buildConfigField 'Integer', 'CODE', '1234'
            buildConfigField 'String', 'HASS_URL', '""'
            buildConfigField 'String', 'BROKER', '""'
            buildConfigField 'String', 'BROKER_USERNAME', '""'
            buildConfigField 'String', 'BROKER_PASS', '""'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    packaging {
        resources {
            excludes += ['META-INF/INDEX.LIST', 'META-INF/io.netty.versions.properties']
        }
    }
    namespace 'xyz.wallpanel.app'
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    // Vision API deprecated; ML Kit is the replacement (migration in progress)
    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'com.google.mlkit:barcode-scanning:17.2.0'
    implementation 'com.google.mlkit:face-detection:16.1.6'

    // Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.2'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // LiveData + ViewModel (lifecycle-extensions is deprecated; use specific artifacts)
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-service:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versions.lifecycle"
    implementation 'com.google.firebase:firebase-analytics:21.1.1'
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit"

    // OKHttp
    implementation 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    implementation 'com.squareup.okhttp3:okhttp:3.12.13'

    // Dagger 2.52+ for Kotlin 2.0 metadata support
    implementation "com.google.dagger:dagger:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"
    implementation "com.google.dagger:dagger-android-support:$versions.dagger"
    implementation "com.google.dagger:dagger-android:$versions.dagger"
    kapt "com.google.dagger:dagger-android-processor:$versions.dagger"

    // MQTT
    implementation 'com.hivemq:hivemq-mqtt-client:1.3.2'
    implementation 'com.koushikdutta.async:androidasync:2.1.9'
    implementation 'net.sourceforge.streamsupport:android-retrostreams:1.7.4'
    implementation 'net.sourceforge.streamsupport:android-retrofuture:1.7.4'

    // Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Glide for image loading (Picasso removed; use Glide only)
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    // LeakCanary only in debug to avoid overhead in release
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'

    //  _____ _          _
    // |  ___(_)_ __ ___| |__   __ _ ___  ___
    // | |_  | | '__/ _ \ '_ \ / _` / __|/ _ \
    // |  _| | | | |  __/ |_) | (_| \__ \  __/
    // |_|   |_|_|  \___|_.__/ \__,_|___/\___|
    //
    implementation 'com.google.firebase:firebase-core:21.1.1'

    // Import the Firebase BoM (see: https://firebase.google.com/docs/android/learn-more#bom)
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation platform('com.google.firebase:firebase-bom:29.3.1')

    // For an optimal experience using Crashlytics, add the Firebase SDK
    // for Google Analytics. This is recommended, but not required.
    implementation 'com.google.firebase:firebase-analytics'
    // Firebase Crashlytics (Kotlin)
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    // Unit tests
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestUtil 'androidx.test:orchestrator:1.4.1'

}

// Apply Google Services and Crashlytics only when google-services.json exists (e.g. from Firebase console)
if (file('google-services.json').exists()) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
}

// Removed: force "com.android.support:appcompat-v7" - project uses AndroidX only

/*configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '25.3.0'
            }
        }
    }
}*/

// Disable lint tasks when building on JDK 25+ (lint worker fails with "25.0.2" javacOptions bug)
tasks.configureEach { t -> if (t.name.startsWith('lint')) t.enabled = false }
